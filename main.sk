local function UpdateTeam(p: player):
    set {_color::*} to light red, orange, yellow, lime, aqua and magenta
    if {StarlightLife::Data::%{_p}'s uuid%::Lives} > 6:
        set team color of team of {_p} to magenta
    else if {StarlightLife::Data::%{_p}'s uuid%::lives} <= 0:
        set team color of team of {_p} to gray
    else:
        set team color of team of {_p} to {_color::%{StarlightLife::Data::%{_p}'s uuid%::Lives}%}

local function RollRoles():
    clear {StarlightLife::Roles::*}
    loop 3 times:
        send title "&a&l%4 - loop-iteration%" to all players
        play sound "ui.button.click" to all players
        wait 2 seconds
    send title "&7&lYou are..." to all players
    play sound "entity.breeze.inhale" to all players
    set {_p::*} to all players where [{StarlightLife::Data::%input's uuid%::Lives} > 1]
    loop (a random integer between 1 and 3) times:
        set {StarlightLife::Roles::DuskBringer::%loop-iteration%} to a random player out of {_p::*}
        remove {StarlightLife::Roles::DuskBringer::%loop-iteration%} from {_p::*}
    loop (a random integer between 1 and 2) times:
        set {StarlightLife::Roles::DawnBringer::%loop-iteration%} to a random player out of {_p::*}
        remove {StarlightLife::Roles::DawnBringer::%loop-iteration%} from {_p::*}
    wait 2 seconds
    send title "&5&lA Duskbringer" to {StarlightLife::Roles::DuskBringer::*}
    send "&7You are &5&lA Duskbringer&r. &7You must kill another player by the end of the session to be cured! If you &cfail &7you will &close 75☆" to {StarlightLife::Roles::DuskBringer::*}
    play sound "block.note_block.didgeridoo" to {StarlightLife::Roles::DawnBringer::*} and {StarlightLife::Roles::DuskBringer::*}
    send title "&d&lA Dawnbringer" to {StarlightLife::Roles::DawnBringer::*}
    send "&7You are &d&lA Dawnbringer&r. &7Every 10 seconds you will transfer starlight from yourself to nearby players. This cannot be cured." to {StarlightLife::Roles::DawnBringer::*}
    send title "&a&lNot Special" to all players where [{StarlightLife::Roles::DawnBringer::*} and {StarlightLife::Roles::DuskBringer::*} does not contain input]

local function Eliminate(p: player, dm: string = "none"):
    send title "&c&lYou have run out of lives!" to {_p} 
    strike lightning effect at {_p}
    drop items in {_p}'s inventory at {_p} if gamerule keepInventory of world of {_p} is true
    set gamemode of {_p} to spectator
    broadcast {_dm} if {_dm} != "none"
    wait 1 tick
    send "&4You have been eliminated! Better luck next time." to {_p}

local function FailRoles():
    loop {StarlightLife::Roles::DuskBringer::*}:
        remove 75 from {StarlightLife::Data::%loop-value's uuid%::Starlight}
        if {StarlightLife::Data::%loop-value's uuid%::Starlight} <= 0:
            remove 1 from {StarlightLife::Data::%loop-value's uuid%::Lives}
            add 100 to {StarlightLife::Data::%loop-value's uuid%::Starlight} 
            if {StarlightLife::Data::%loop-value's uuid%::Lives} = 0:
                Eliminate(loop-value, "&c%loop-value% has been eliminated!")
        send title "&cYou have failed!" with subtitle "&c-75☆" to loop-value
        send "&4You have failed to cure yourself. Therefore you have lost 75 starlight." to loop-value
    clear {StarlightLife::Roles::*}

local function Start():
    loop 3 times:
        send title "&a&l%4 - loop-iteration%" to all players
        play sound "ui.button.click" to all players 
        wait 1 second
    send title "&a&lThe Stars Shine Bright" to all players
    play sound "item.goat_horn.sound.7" to all players
    loop all players:
        set {StarlightLife::Data::%loop-player's uuid%::Starlight} to 100
        set {StarlightLife::Data::%loop-player's uuid%::Lives} to 6
        UpdateTeam(loop-player)
on join:
    if team of player isn't set:
        add player to team named "%player%"
        UpdateTeam(player)

on load:
    loop all players where [team of input isn't set]:
        add loop-player to team named "%loop-player%"
        UpdateTeam(loop-player)
        

on death of player:
    {StarlightLife::Session} = true
    remove 25 from {StarlightLife::Data::%victim's uuid%::Starlight}
    send title "&c-25☆" to victim
    if victim != attacker:
        add 10 to {StarlightLife::Data::%attacker's uuid%::Starlight}
        send title "&a+10☆" to attacker
        if {StarlightLife::Roles::DuskBringer::*} contains attacker:
            remove 25 from {StarlightLife::Data::%victim's uuid%::Starlight}
            send title "&c-50☆" with subtitle "&c%attacker% has been cured!" to victim
            play sound "block.note_block.didgeridoo" to victim
            add 10 to {StarlightLife::Data::%attacker's uuid%::Starlight}
            send title "&a+20☆" with subtitle "&aYou are cured!" to attacker
            play sound "block.note_block.bell" to attacker
            remove attacker from {StarlightLife::Roles::DuskBringer::*}
    if {StarlightLife::Data::%victim's uuid%::Starlight} <= 0:
        remove 1 from {StarlightLife::Data::%victim's uuid%::Lives}
        add 100 to {StarlightLife::Data::%victim's uuid%::Starlight}
        UpdateTeam(victim)
        if {StarlightLife::Data::%victim's uuid%::Lives} = 0:
            Eliminate(victim)
            set death message to "&c%uncolored death message%"


every second:
    {StarlightLife::Session} = true
    loop all players where [{StarlightLife::Data::%input's uuid%::Lives} >= 1]:
        light level at loop-player <= 4
        if {StarlightLife::Timer::%loop-player's uuid%} isn't set:
            set {StarlightLife::Timer::%loop-player's uuid%} to a random integer between 1 and 5
        remove 1 from {StarlightLife::Timer::%loop-player's uuid%}
        if {StarlightLife::Timer::%loop-player's uuid%} = 0:
            set {StarlightLife::Timer::%loop-player's uuid%} to a random integer between 1 and 5
            remove (a random integer between 1 and 5) from {StarlightLife::Data::%loop-player's uuid%::Starlight} if {StarlightLife::Data::%loop-player's uuid%::Lives} > 1
            remove (a random integer between 1 and 3) from {StarlightLife::Data::%loop-player's uuid%::Starlight} if {StarlightLife::Data::%loop-player's uuid%::Lives} = 1
            if {StarlightLife::Data::%loop-player's uuid%::Starlight} <= 0:
                remove 1 from {StarlightLife::Data::%loop-player's uuid%::Lives}
                add 100 to {StarlightLife::Data::%loop-player's uuid%::Starlight}
                UpdateTeam(loop-player)
                if {StarlightLife::Data::%loop-player's uuid%::Lives} = 0:
                    Eliminate(loop-player, "&c%loop-player% has been eliminated!")
      
every 5 seconds:
    {StarlightLife::Session} = true
    loop {StarlightLife::Roles::DuskBringer::*}:
        loop players in radius 5 of loop-value where [{StarlightLife::Data::%input's uuid%::Lives} >= 1]:
            remove 1 from {StarlightLife::Data::%loop-player's uuid%::Starlight}
            add 1 to {StarlightLife::Data::%loop-value-1's uuid%::Starlight}
            if {StarlightLife::Data::%loop-player's uuid%::Starlight} <= 0:
                remove 1 from {StarlightLife::Data::%loop-player's uuid%::Lives}
                add 100 to {StarlightLife::Data::%loop-player's uuid%::Starlight}
                UpdateTeam(loop-player)
                if {StarlightLife::Data::%loop-player's uuid%::Lives} = 0:
                    Eliminate(loop-player, "&c%loop-player% has been eliminated!")

every 10 minutes:
    loop {StarlightLife::Roles::DuskBringer::*}:
        apply darkness without particles to loop-value for 1 minute
        remove (a random integer between 1 and 3) from {StarlightLife::Data::%loop-value's uuid%::Starlight}
        if {StarlightLife::Data::%loop-value's uuid%::Starlight} <= 0:
            remove 1 from {StarlightLife::Data::%loop-value's uuid%::Lives}
            add 100 to {StarlightLife::Data::%loop-value's uuid%::Starlight}
            UpdateTeam(loop-value)
            if {StarlightLife::Data::%loop-value's uuid%::Lives} = 0:
                Eliminate(loop-value, "&c%loop-value% has been eliminated!")

every 10 seconds:
    {StarlightLife::Session} = true
    loop {StarlightLife::Roles::DawnBringer::*}:
        loop players in radius 5 of loop-value where [{StarlightLife::Data::%input's uuid%::Lives} >= 1]:
            remove 1 from {StarlightLife::Data::%loop-value-1's uuid%::Starlight}
            add 1 to {StarlightLife::Data::%loop-value-2's uuid%::Starlight}
            if {StarlightLife::Data::%loop-value-1's uuid%::Starlight} <= 0:
                remove 1 from {StarlightLife::Data::%loop-value-1's uuid%::Lives}
                add 100 to {StarlightLife::Data::%loop-value-1's uuid%::Starlight} 
                UpdateTeam(loop-value-1)
                if {StarlightLife::Data::%loop-value-1's uuid%::Lives} = 0:
                    Eliminate(loop-value-1, "&c%loop-value-1% has been eliminated!")





every 5 ticks:
    loop all players where [{StarlightLife::Data::%input's uuid%::Lives} > 0]:
        send actionbar formatted "<%team color of team of loop-player%>%{StarlightLife::Data::%loop-player's uuid%::Starlight}% ☆" to loop-player
      
     
on tab complete of "/starlightlife", "/sl", "/life:starlightlife" and "/life:sl":
    set tab completions for position 1 to "modify", "session", "roles" and "reset"
    if tab arg-1 = "session":
        set tab completions for position 2 to "start", "end" and "pause"
    else if tab arg-1 = "roles":
        set tab completions for position 2 to "check", "set", "fail" and "roll"
        if tab arg-2 = "check" or "set":
            set tab completions for position 3 to all players
            if tab arg-2 = "set":
                set tab completions for position 4 to "duskbringer", "dawnbringer" and "none"
    else if tab arg-1 = "modify":
        set tab completions for position 2 to "lives", "starlight" and "eliminate"
        set tab completions for position 3 to all players
        if tab arg-2 = "lives" or "starlight":
            set tab completions for position 4 to "set", "remove", "check" and "add"
            


command /starlightlife [<text>] [<text>] [<text>] [<text>] [<text>] [<text>]:
    permission: op
    aliases: sl
    prefix: life
    trigger:
        if arg-1 = "reset":
            if arg-2 = "confirm":
                delete {StarlightLife::*}
                broadcast "&c&lThis Season's save data has been cleared by %sender%"
                wait 5 seconds
                set {_b} to mini message from "<green><click:run_command:'/life:starlightlife start'>Click here to start a new season."
                if sender is a player:
                    send component {_b}
                else:	
                    send component {_b} to all operators
            else:
                send formatted "<red>You are about to delete all save data for this season."
                wait 1 second
                send component mini message from "<red>This action is <b>PERMANENT</b>!"
                wait 1 second
                send formatted "<red>Run <underline>/%command% reset confirm.<reset><red> To complete the reset." 
        else if arg-1 = "start":
            send "&a&lA new Season has been started by %sender%" to all operators
            set {StarlightLife::Session} to true
            Start()
        else if arg-1 = "session":
            if arg-2 = "start":
                broadcast "&a&lThe Session has startedd!"
                play sound "block.beacon.activate" to all players
                set {StarlightLife::Session} to true
            else if arg-2 = "end":
                FailRoles()
                broadcast "&c&lThe Session has ended!"
                play sound "block.beacon.deactivate" to all players
                set {StarlightLife::Session} to false
            else if arg-2 = "pause":
                if {StarlightLife::Sesssion} = true:
                    broadcast "&c&lThe Session has been paused!"
                    play sound "block.beacon.deactivate" to all players
                if {StarlightLife::Sesssion} = false:
                    broadcast "&a&lThe Session has been resumed!"
                    play sound "block.beacon.activate" to all players
                set {StarlightLife::Session} to inverse of {StarlightLife::Session}
        else if arg-1 = "modify":
            set {_p} to arg-3 parsed as player
            set {_color::*} to light red, orange, yellow, lime, aqua and magenta
            set {_lives} to 6
            set {_lives} to {StarlightLife::Data::%{_p}'s uuid%::Lives} if {StarlightLife::Data::%{_p}'s uuid%::Lives} <= 6
            if arg-2 = "lives" or "starlight":
                if arg-4 = "check":
                    send formatted "<%team color of team of {_p}%>%{_p}%<light grey> has <%{_color::%{_lives}%}%>%{StarlightLife::Data::%{_p}'s uuid%::%strict proper case arg-2%}% <light grey>%strict proper case arg-2%."
                else if arg-4 = "set", "add" or "remove":
                    set {_i} to arg-5 parsed as integer
                    if arg-4 = "set":
                        set {StarlightLife::Data::%{_p}'s uuid%::%strict proper case arg-2%} to {_i}
                    else if arg-4 = "add":
                        add {_i} to {StarlightLife::Data::%{_p}'s uuid%::%strict proper case arg-2%}
                     
                    else if arg-4 = "remove":
                        add {_i} to {StarlightLife::Data::%{_p}'s uuid%::%strict proper case arg-2%}
                if {StarlightLife::Data::%{_p}'s uuid%::Starlight} <= 0:
                    remove 1 from {StarlightLife::Data::%{_p}'s uuid%::Lives}
                    add 100 to {StarlightLife::Data::%{_p}'s uuid%::Starlight} 
                    if {StarlightLife::Data::%{_p}'s uuid%::Lives} = 0:
                        Eliminate({_p}, "&c%{_p}% has been eliminated!")
                UpdateTeam({_p})
                send "&a%{_p}% now has %{StarlightLife::Data::%{_p}'s uuid%::%strict proper case arg-2%}% %arg-2%"
            else if arg-2 = "eliminate":
                Eliminate({_p}, "&c%{_p}% has been eliminated!")
        else if arg-1 = "roles":
            if arg-2 = "roll":
                send "&aRolling roles..."
                RollRoles()
            else if arg-2 = "fail":
                FailRoles()
            else if arg-2 = "check" or "set":
                set {_p} to arg-3 parsed as player
                if arg-2 = "check":
                    if {StarlightLife::Roles::DuskBringer::*} contains {_p}:
                        send formatted "<%team color of team of {_p}%>%{_p}% is &5&lA Duskbringer."
                    else if {StarlightLife::Roles::DawnBringer::*} contains {_p}:
                        send formatted "<%team color of team of {_p}%>%{_p}%&7 is &d&lA Dawnbringer."
                    else:
                        send formatted "<%team color of team of {_p}%>%{_p}%&7 does not have a role."
                else:
                    if {StarlightLife::Roles::DuskBringer::*} contains {_p}:
                        remove {_p} from {StarlightLife::Roles::DuskBringer::*}
                        remove {_p} from {StarlightLife::Roles::DawnBringer::*}
                        if arg-4 is "none":
                            send "&aYour role was removed" to {_p}
                            send "&a%{_p}% no longer has a role."
                            stop
                    add {_p} to {StarlightLife::Roles::%proper case arg-4%}
                    if {StarlightLife::Roles::DuskBringer::*} contains {_p}:
                        send "&7You are &5&lA Duskbringer&r. &7You must kill another player by the end of the session to be cured! If you &cfail &7you will &close 75☆" to {_p} 
                    else if {StarlightLife::Roles::DawnBringer::*} contains {_p}:
                        send "&7You are &d&lA Dawnbringer&r. &7Every 10 seconds you will transfer starlight from yourself to nearby players. This cannot be cured." to {_p}
                    send "&a%{_p}%'s role has been set to %proper case arg-4% by %sender%"
                                
            
command /claim [<player>]:
    prefix: life
    trigger:
        set {_end} to "!"
        if arg-1 is set:
            set {_end} to " on %arg-1%!"
        if player != arg-1:
            add 10 to {StarlightLife::Data::%player's uuid%::Starlight}
            send title "&a+10☆" to player
            if all: 
                {StarlightLife::Roles::DuskBringer::*} contains player
                arg-1 is set
            then:
                remove 25 from {StarlightLife::Data::%arg-1's uuid%::Starlight}
                send title "&c-25☆" with subtitle "&c%player% has been cured!" to arg-1
                if {StarlightLife::Data::%arg-1's uuid%::Starlight} <= 0:
                    remove 1 from {StarlightLife::Data::%arg-1's uuid%::Lives}
                    add 100 to {StarlightLife::Data::%arg-1's uuid%::Starlight}
                    UpdateTeam(arg-1)
                    if {StarlightLife::Data::%arg-1's uuid%::Lives} = 0:
                        Eliminate(arg-1, "&c%arg-1% has been eliminated!")
                add 10 to {StarlightLife::Data::%player's uuid%::Starlight}
                send title "&a+20☆" with subtitle "&aYou are cured!" to player
                remove player from {StarlightLife::Roles::DuskBringer::*}
            broadcast "&a%player% has claimed a kill%{_end}%"
            stop
        send "&cYou cannot claim a kill on yourself."
        